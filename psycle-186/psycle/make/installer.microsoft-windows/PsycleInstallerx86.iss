; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
#include <it_download.iss>

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8E7D0A7F-B85F-44DC-8C1C-2A2C27BAEA0B}
AppName=Psycle Modular Music Creation Studio
AppVersion=1.8.8 beta 2
;AppVerName=Psycle Modular Music Creation Studio 1.8.8
AppPublisher=psycledelics
AppPublisherURL=http://psycle.sourceforge.net/
AppSupportURL=http://psycle.sourceforge.net/
AppUpdatesURL=http://psycle.sourceforge.net/
DefaultDirName={pf}\Psycle Modular Music Studio
DefaultGroupName=Psycle
AllowNoIcons=true
InfoBeforeFile=..\..\doc\for-end-users\readme.txt
InfoAfterFile=..\..\doc\for-end-users\whatsnew.txt
OutputBaseFilename=PsycleInstallerx86
SetupIconFile=..\..\pixmaps\psycle.ico
Compression=lzma
SolidCompression=true
MinVersion=0,5.0.2195sp4
AppCopyright=2000-2010 psycledelics
AppVerName=Psycle 1.8.8
PrivilegesRequired=poweruser
TimeStampsInUTC=true
DisableReadyPage=true
ArchitecturesInstallIn64BitMode=
ArchitecturesAllowed=x86 x64
ChangesAssociations=true

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: deleteCacheFile; Description: deletes the cache of plugin names to force it to be regenerated.; Languages: ; Flags: unchecked
Name: deleteRegistrySettings; Description: deletes the existing settings in the registry, allowing psycle to regenerate the defaults.; Flags: unchecked

[Files]
Source: ..\msvc_9.0\output\Win32\release\bin\psycle.exe; DestDir: {app}; Flags: ignoreversion 32bit; Components: Application
Source: ..\msvc_9.0\output\Win32\release\bin\boost_date_time-vc90-mt-1_41.dll; DestDir: {app}; Flags: ignoreversion 32bit; Components: Application
Source: ..\msvc_9.0\output\Win32\release\bin\boost_filesystem-vc90-mt-1_41.dll; DestDir: {app}; Flags: ignoreversion 32bit; Components: Application
Source: ..\msvc_9.0\output\Win32\release\bin\boost_system-vc90-mt-1_41.dll; DestDir: {app}; Flags: ignoreversion 32bit; Components: Application
Source: ..\msvc_9.0\output\Win32\release\bin\boost_thread-vc90-mt-1_41.dll; DestDir: {app}; Flags: ignoreversion 32bit; Components: Application
Source: ..\msvc_9.0\output\Win32\release\bin\universalis.dll; DestDir: {app}; Flags: ignoreversion 32bit; Components: Application
Source: ..\..\..\..\psycleSVN\build-systems\msvc\output\Win32\release\bin\psycle-plugins\*.dll; DestDir: {app}\PsyclePlugins; Flags: ignoreversion 32bit; Tasks: ; Languages: ; Excludes: crasher.dll; Components: " Open_Source_Plugins"
Source: ..\..\doc\for-end-users\*; DestDir: {app}\docs; Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: .svn, Log1.log; Components: " Documentation"
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: ..\..\closed-source\*.dll; DestDir: {app}\PsyclePlugins\closed-source; Flags: recursesubdirs createallsubdirs 32bit; Components: Closed_Source_Plugins
Source: ..\..\..\..\psycleSVN\psycle-plugins\src\psycle\plugins\*.txt; DestDir: {app}\Docs; Excludes: license.txt; Flags: recursesubdirs ignoreversion; Components: " Documentation"
Source: ..\..\Skins\*; DestDir: {app}\Skins; Excludes: *.txt; Flags: recursesubdirs ignoreversion createallsubdirs; Components: " Skins"
Source: ..\..\doc\*.psy; DestDir: {commondocs}\Psycle Songs; Flags: ignoreversion
Source: ..\..\..\..\psycleSVN\psycle-plugins\presets\*.prs; DestDir: {app}\PsyclePlugins; Flags: ignoreversion onlyifdoesntexist; Components: Presets

[Icons]
Name: {group}\Psycle Modular Music Creation Studio; Filename: {app}\psycle.exe
Name: {group}\{cm:ProgramOnTheWeb,Psycle Modular Music Creation Studio}; Filename: http://psycle.sourceforge.net/
Name: {group}\{cm:UninstallProgram,Psycle Modular Music Creation Studio}; Filename: {uninstallexe}
Name: {commondesktop}\Psycle Modular Music Creation Studio; Filename: {app}\psycle.exe; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\Psycle Modular Music Creation Studio; Filename: {app}\psycle.exe; Tasks: quicklaunchicon
Name: {group}\Documents\How to Skin Psycle; Filename: {app}\Docs\how to skin psycle.txt; WorkingDir: {app}\Docs; Components: Documentation
Name: {group}\Documents\Version History; Filename: {app}\Docs\whatsnew.txt; WorkingDir: {app}\Docs; Components: Documentation
Name: {group}\Documents\Pattern Keys; Filename: {app}\Docs\keys.txt; WorkingDir: {app}\Docs; Components: Documentation
Name: {group}\Documents\MIDI Help; Filename: {app}\Docs\MIDI Help\psyclemidi.html; WorkingDir: {app}\Docs\MIDI Help; Components: Documentation
Name: {group}\Documents\Upwego Tutorial; Filename: {app}\Docs\Upwego\upwego5.html; WorkingDir: {app}\Docs\Upwego; Components: Documentation
Name: {group}\Documents\Tweakings And Commands; Filename: {app}\Docs\tweakings and commands.txt; WorkingDir: {app}\Docs\; Components: 

[Run]
Filename: {app}\psycle.exe; Description: {cm:LaunchProgram,Psycle Modular Music Creation Studio}; Flags: nowait postinstall skipifsilent; Tasks: ; Components: Application
Filename: {tmp}\Vst-Bundle.exe; WorkingDir: {tmp}; StatusMsg: Select the location of your 32bit VST Plugins Dir (use Psycle\VstPlugins if in doubt); Flags: runascurrentuser; Components: " VstPack"; Tasks: ; Languages: 
Filename: {tmp}\vcredist_x86.exe; Flags: 32bit runascurrentuser; WorkingDir: {tmp}; Components: InstallMSRuntimes_x86

[Components]
Name: Application; Description: Main Application and needed dlls; Flags: fixed; Types: custom compact full; Languages: 
Name: InstallMSRuntimes_x86; Description: Download and Install Microsoft Visual c++ 2008 runtimes (x86); Types: custom full; ExtraDiskSpaceRequired: 5000000; Languages: ; Check: Is32bitInstallMode
Name: Open_Source_Plugins; Description: Install those plugins which the developer made their sources available; Types: custom compact full
Name: Closed_Source_Plugins; Description: Install those plugins of which we don't have the sources; Types: custom full
Name: VstPack; Description: Download and install the freely available VST Pack.; Types: custom full; ExtraDiskSpaceRequired: 19038208
Name: Presets; Description: Install presets for selected plugins (does not overwrite existing files); Types: custom full
Name: Documentation; Description: Install the documentation of the project; Types: custom full
Name: Skins; Description: Skins that change the look and feel of Psycle; Types: custom full
Name: Demo_Songs; Description: Demo songs to show what psycle can do; Types: custom full; Languages: 
[Dirs]
Name: {app}\PsyclePlugins; Flags: uninsalwaysuninstall; Components: " Open_Source_Plugins Closed_Source_Plugins"; Tasks: ; Languages: 
Name: {app}\Skins; Flags: uninsalwaysuninstall; Components: " Skins"
Name: {app}\VstPlugins; Flags: deleteafterinstall; Components: Application; Tasks: 
Name: {app}\VstPlugins64; Flags: uninsalwaysuninstall; Components: Application; Tasks: ; Languages: 
Name: {app}\Docs; Flags: uninsalwaysuninstall; Components: Documentation
Name: {commondocs}\Psycle Songs; Components: " Demo_Songs"
[Registry]
Root: HKCU; Subkey: software\psycle; Flags: uninsdeletekey
Root: HKCU; Subkey: software\psycle; Tasks: " deleteRegistrySettings"; Flags: deletekey
Root: HKCU; Subkey: software\AAS\Psycle; Flags: deletekey; Tasks: " deleteRegistrySettings"
Root: HKCR; Subkey: .psy; ValueType: string; ValueData: Psycle.Music.File; Flags: uninsdeletekey
Root: HKCR; SubKey: Psycle.Music.File; ValueType: none; Flags: uninsdeletekey
Root: HKCR; SubKey: Psycle.Music.File\Shell\Open\Command; ValueType: string; ValueData: """{app}\psycle.exe"" ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: Psycle.Music.File\DefaultIcon; ValueType: string; ValueData: """{app}\psycle.exe"",0"; Flags: uninsdeletevalue
[InstallDelete]
Name: {userappdata}\..\.psycle\; Type: filesandordirs; Tasks: " deleteCacheFile"; Components: Application
Name: {win}\Psyclekeys.ini; Type: files; Tasks: " deleteCacheFile"; Components: Application
Name: {userappdata}\psycle\; Type: filesandordirs; Tasks: " deleteCacheFile"; Components: Application
[UninstallDelete]
Name: {userappdata}\..\.psycle\; Type: filesandordirs; Components: Application
Name: {win}\Psyclekeys.ini; Type: files; Components: Application
Name: {userappdata}\psycle\; Type: filesandordirs; Components: Application
[Code]
procedure InitializeWizard();
begin
  ITD_Init();
  ITD_DownloadAfter(wpReady);
end;

function NextButtonClick(CurPage: Integer): Boolean;
begin
  if CurPage = wpSelectTasks then
  begin
    ITD_ClearFiles();
    if IsComponentSelected('VstPack') then
    begin
       ITD_AddFile('http://heanet.dl.sourceforge.net/project/psycle/psycle/1.8.5/psycle-1.8.5-vst-bundle.exe',ExpandConstant('{tmp}\Vst-Bundle.exe'));
    end;
    if IsComponentSelected('InstallMSRuntimes_x86') then
    begin
      ITD_AddFile('http://download.microsoft.com/download/d/d/9/dd9a82d0-52ef-40db-8dab-795376989c03/vcredist_x86.exe',ExpandConstant('{tmp}\vcredist_x86.exe'));
    end;
  end;
  Result := True;
end;

function Is32bitInstallMode() : Boolean;
begin
	if Is64BitInstallMode then
		Result := False
	else
		Result := True;
end;
